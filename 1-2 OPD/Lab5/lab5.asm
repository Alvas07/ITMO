ORG 0x57B ; Программа начинается с адреса 57B
STR_ADDR: WORD 0x635 ; Размещаемая строка находится по адресу 635
POINTER: WORD ? ; Указатель на текущие 2 символа строки
EOF: WORD 0x0D ; Стоп-символ

START: 		CLA ; Очищаем аккумулятор
			LD STR_ADDR ; Загружаем адрес начала строки
			ST POINTER ; Инициализируем указатель адресом начала строки

S1: 		IN 7 ; Ожидаем готовность ВУ-3
			AND #0x40 ; Проверяем готовность ВУ-3 (Бит 6 SR)
			BEQ S1 ; Если SR == 0 (не готов), то спин-луп
			LD (POINTER) ; Загружаем текущие 2 символа строки (косвенная относительная адресация)
			OUT 6 ; Выводим младший байт (первый символ) на ВУ-3

			SXTB ; Расширяем знак для проверки на EOF
			CMP EOF ; Устанавливаем флаги для сравнения текущего символа с EOF
			BEQ STOP ; Если EOF, то завершаем программу

S2: 		IN 7 ; Ожидаем готовность ВУ-3
			AND #0x40 ; Проверяем готовность ВУ-3 (Бит 6 SR)
			BEQ S2 ; Если SR == 0 (не готов), то спин-луп
			LD (POINTER)+ ; Загружаем текущие 2 символа строки и перемещаем указатель на следующие 2 (косвенная автоинкремент)
			SWAB ; Меняем местами старший и младший байты (2 и 1 символы)
			OUT 6 ; Выводим младший байт (второй символ) на ВУ-3

			SXTB ; Расширяем знак для проверки на EOF
			CMP EOF ; Устанавливаем флаги для сравнения текущего символа с EOF
			BEQ STOP ; Если EOF, то завершаем программу
			JUMP S1 ; Повторяем обработку 2 символов

STOP: 		HLT ; Завершение программы

ORG 0x635 ; Размещаемая строка находится по адресу 635
WORD 0xC1ED ; Ма
WORD 0x0DCD ; м(CR)
